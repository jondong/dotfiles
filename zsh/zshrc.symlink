# Path to your oh-my-zsh installation.
export ZSH=$HOME/.oh-my-zsh

if [ "$(uname -o)" = "Cygwin" ]; then
  ZSH_THEME="babun"

  plugins=(git)

  # User configuration

  export PATH=$HOME/bin:/usr/local/bin:$PATH
  # export MANPATH="/usr/local/man:$MANPATH"

  source $ZSH/oh-my-zsh.sh
fi


# shortcut to this dotfiles path is $ZSH
export DOTFILES=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=$HOME/projects

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

if [ "$(uname -o)" != "Cygwin" ]; then
  # initialize autocomplete here, otherwise functions won't be loaded
  autoload -U compinit
  compinit
fi

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# added by travis gem
[ -f /home/jonathan/.travis/travis.sh ] && source /home/jonathan/.travis/travis.sh
