# Path to your oh-my-zsh installation.

export ZSH=$HOME/.oh-my-zsh

if [ $PLATFORM = "Cygwin" ]; then
  ZSH_THEME="babun"

  plugins=(git)

  # User configuration

  export PATH=$HOME/bin:/usr/local/bin:$PATH

  source $ZSH/oh-my-zsh.sh
fi

# all of our zsh files
shells_zsh=($DOTFILES_ROOT/shells/zsh/**/*.zsh)
platform_zsh=

if [ $PLATFORM = "Darwin" ]; then
    platform_zsh=($DOTFILES_ROOT/platforms/mac/**/*.zsh)
elif [ $PLATFORM = "Linux" ]; then
    platform_zsh=($DOTFILES_ROOT/platforms/linux/**/*.zsh)
elif [ $PLATFORM = "Cygwin" ]; then
    platform_zsh=($DOTFILES_ROOT/platforms/win/**/*.zsh)
fi
typeset -U config_files
config_files=(${shells_zsh[@]} ${platform_zsh[@]})

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

if [ $PLATFORM != "Cygwin" ]; then
  # initialize autocomplete here, otherwise functions won't be loaded
  autoload -U compinit
  compinit
fi

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# z configuration
. /usr/local/etc/profile.d/z.sh

# bindkey with Emacs style. This is for tmux-yank plugin.
bindkey -e

# Load local zshrc if it exists. This should stay at the end of .zshrc
# to give user a chance to rewrite the default settings.
[ -f ~/.local.zshrc ] && source ~/.local.zshrc

test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"
[ -f ~/.tbenv/bundler-exec.sh ] && source ~/.tbenv/bundler-exec.sh

export NVM_DIR="$HOME/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"  # This loads nvm
[ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"  # This loads nvm bash_completion
